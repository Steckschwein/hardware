;
;	gfx mode 1 - 40x24 chars - text mode
;
*=$1000
    jmp main
    
!src <defs.h.a>
!src <bios.h.a>
!src <t99xx.lib.a>
.charset_6x8
!src "../charsets/char.ascii.vc20.6x8.h.a"
main
    sei
    
	jsr	vdp_display_off			;display off

	;+SetVector	charset,	adrl	
	;jsr	.mirror_charset				;inverse the charset
	;jsr	.rotate_charset

    jsr vdp_mode_sprites_off
    jsr vdp_mode_text_blank
    
	+SetVector	.charset_6x8,	addr
    lda #<ADDRESS_GFX1_PATTERN
    ldy #.WRITE_ADDRESS + >ADDRESS_GFX1_PATTERN
	ldx	#$08
	jsr	vdp_memcpy

	lda	#<ADDRESS_GFX1_SCREEN
	ldy	#.WRITE_ADDRESS + >ADDRESS_GFX1_SCREEN
	jsr	vdp_sreg
	ldx	#$00
-	stx	a_vram
	inx	
	bne	-
	
	lda	#<ADDRESS_GFX1_SCREEN+(40*16)
	ldy	#.WRITE_ADDRESS + >ADDRESS_GFX1_SCREEN + (40*16)
	jsr	vdp_sreg
	ldx	#$00
-	lda	.msg,x
	beq	+
	sta	a_vram
	inx	
	bne	-
+	
	jsr vdp_mode_text			;enable text mode

-	jmp	-

tmp8=$f8
tmp9=$f9
tmp10=$fa
	
.rotate_charset
	+SetVector .charset_6x8, adrl	;setup vram with charset
	+SetVector .charset, tmp8	;setup vram with charset
	ldx	#$08
	ldy	#$00
-	lda	(adrl),y
	sta (tmp8),y
	iny
	bne	-
	inc	adrh
	inc	tmp9
	dex
	bne	-
	+SetVector .charset, adrl

	lda	#$08
	sta	tmp10
	lda	#$00
	stz	tmp0
	stz	tmp1
	stz	tmp2
	stz	tmp3
	stz	tmp4
	stz	tmp5
	stz	tmp6
	stz	tmp7
	lda	#$80
	sta	tmp9
--	lda	#$01
	sta	tmp8
	ldx	#$00
-	lda	(adrl),y
	and	tmp8
	beq	+
	lda	tmp0,x
	ora	tmp9
	sta	tmp0,x
+	asl	tmp8
	inx
	cpx	#$08
	bne	-
	lsr	tmp9
	bcc	+
	lda	#$80
	sta	tmp9
	phy
	tya
	sec	
	sbc	#$07
	tay	
	ldx	#$00
-	lda	tmp0,x
	sta	(adrl),y
	stz	tmp0,x
	iny
	inx
	cpx	#$08
	bne	-
	ply
+	iny
	bne	--
	inc	adrh
	dec	tmp10
	bne	--
	rts
	
	
.mirror_charset
	ldx	#$08
	ldy	#$00
-	stz	tmp0
	lda	(adrl),y	;bit inverse
	asl
	ror	tmp0
	asl
	ror	tmp0
	asl
	ror	tmp0
	asl
	ror	tmp0
	asl
	ror	tmp0
	asl
	ror	tmp0	
	lda	tmp0
	sta	(adrl),y
	iny
	bne	-
	inc	adrh
	dex
	bne	-
	rts

.msg	
	!text "This is just a test of the vdp text mode"
	!text "where you can use 40 chars on each line."
	!text "                                        "
	!text "0123456789          0123456789          "
	!text "$AbgKyAqua0123456789          0123456789"
	!text "          0123456789          0123456789"
	!text 224
	!text 0

!align 255,0
.charset
;!src "../charsets/char.ascii.5x8.h.a"		; mirrored
;!src "../charsets/char.ascii.6x8.v4.h.a"	; horizontal
;!src "../charsets/char.ascii.6x8.big.h.a"	; horizontal
